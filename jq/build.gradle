
plugins {
    id 'java'
    id 'antlr'
}

group = 'com.timestored.jq'
version = '0.0.1-SNAPSHOT'


def generatedSources = "$buildDir/generated-src/src/main/java"
sourceSets.main.java.srcDirs = ["$generatedSources",'src/main/java']
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories { mavenCentral() }


 
jar {
    manifest {
        attributes 'Main-Class': 'com.timestored.jq.JQLauncher'
    }
}
dependencies {
    antlr "org.antlr:antlr4:4.8"
    compile "org.antlr:antlr4-runtime:4.8"
	compile "net.sf.jopt-simple:jopt-simple:5.0.4"
//	compile "org.jline:jline:3.14.1"
//	compile "org.fusesource.jansi:jansi:1.18"
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
    outputDirectory = file("$generatedSources")
}


task myPrebuildTask(type: Exec) {
    workingDir "$projectDir/.."
    commandLine 'java', '-jar', 'genie/build/libs/genie-all.jar', "."
}

compileJava {
    dependsOn myPrebuildTask
}